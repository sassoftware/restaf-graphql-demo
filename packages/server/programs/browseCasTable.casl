
 
/*
cas mycas2;   
proc cas;   
 _args_ = {from=1, count=20 caslib="public" name="cars"  format=true};   
*/

results = getData(_args_.caslib, _args_.name , _args_.from, _args_.count,  _args_.format);   
print 'send results';
send_response({casResults = results});  
    
function getData(caslib, name, from, count, format) ;    
 
   action table.tableExists r=result/ 
        caslib = caslib 
        name  = name; 
 
    if ( result.exists eq 0) then do;
       print '---------------------------------- loadtable';
        action table.loadTable status=rc/ 
           caslib = caslib 
           source = upcase(name) || '.sashdat' 
           casout={caslib= caslib name=name};    
           print '------------------------- loadtable completed';
        if ( rc.severity ne 0 ) then do; 
           return {rc=rc}; 
        end; 
    end; 

   print '--------------------- get results';
    action table.tableinfo r= result/    
        caslib = caslib    
        name   = name;    
 
    rowCount = result.TableInfo[1, 'rows'];    
    to       = min(from + count -1, rowCount);    
    
    action table.fetch r = result /    
        table = {caslib=caslib, name=name}   
        from= from to=to format=format   
        ;    
 
    pagePrev = max(from - count, 1);    
    if ( to eq rowCount ) then do;    
        pageNext = -1;    
    end;    
    else do ;    
        pageNext = min(to + 1, rowCount);    
    end;    
   return ({table=result, pagination = {prev=pagePrev, next=pageNext, count=count}, rc=0 }); 
end;    
      